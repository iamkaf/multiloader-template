pluginManagement {
    repositories {
        mavenLocal()
        gradlePluginPortal()
        mavenCentral()
        maven {
            name = 'Fabric'
            url = uri('https://maven.fabricmc.net')
        }
        exclusiveContent {
            forRepository {
                maven {
                    name = 'Sponge'
                    url = uri('https://repo.spongepowered.org/repository/maven-public')
                }
            }
            filter {
                includeGroupAndSubgroups("org.spongepowered")
            }
        }
        exclusiveContent {
            forRepository {
                maven {
                    name = 'Forge'
                    url = uri('https://maven.minecraftforge.net')
                }
            }
            filter {
                includeGroupAndSubgroups('net.minecraftforge')
            }
        }
        // Mod Publisher
        maven { url "https://maven.firstdark.dev/releases" }
    }
}

plugins {
    id 'org.gradle.toolchains.foojay-resolver-convention' version '0.8.0'
}

dependencyResolutionManagement {
    repositories {
        mavenLocal()
        maven { url = uri("https://maven.kaf.sh") }
        mavenCentral()
    }

    versionCatalogs {
        libs {
            def mcVersion = providers.gradleProperty('platform_minecraft_version').get()
            from("com.iamkaf.platform:mc-${mcVersion}:${mcVersion}-SNAPSHOT")
        }
    }
}

// This should match the folder name of the project, or else IDEA may complain (see https://youtrack.jetbrains.com/issue/IDEA-317606)
rootProject.name = mod_name
include('common')
include('fabric')
include('forge')
// include('neoforge')
