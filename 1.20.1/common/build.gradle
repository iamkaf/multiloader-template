plugins {
    id 'multiloader-common'
    id 'net.neoforged.moddev.legacyforge'
}

legacyForge {
    mcpVersion = libs.versions.minecraft.get()
    // Automatically enable AccessTransformers if the file exists
    if (file('src/main/resources/META-INF/accesstransformer.cfg').exists()) {
        accessTransformers = ['src/main/resources/META-INF/accesstransformer.cfg']
    }
    parchment {
        minecraftVersion = libs.versions.parchment.minecraft.get()
        mappingsVersion = libs.parchment.get().version.toString()
    }
}

dependencies {
    compileOnly libs.mixin
    // fabric and forge both bundle mixinextras, so it is safe to use it in common
    compileOnly libs.mixin.extras
    annotationProcessor libs.mixin.extras
    implementation libs.gson

    // Amber integration
    implementation libs.amber
}

configurations {
    commonJava {
        canBeResolved = false
        canBeConsumed = true
    }
    commonResources {
        canBeResolved = false
        canBeConsumed = true
    }
}

artifacts {
    commonJava sourceSets.main.java.sourceDirectories.singleFile
    commonResources sourceSets.main.resources.sourceDirectories.files
}
